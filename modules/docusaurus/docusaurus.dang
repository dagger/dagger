pub description = "Build Docusaurus documentation sites"

"""
A Dagger module for docusaurus, a documentation website builder. https://docusaurus.io
"""
type Docusaurus {
  pub sites(ws: Workspace!): [Site!]! {
    ws
      .directory(".", include: [
        "**/docusaurus.config.ts",
        "**/docusaurus.config.js",
      ])
      .glob("**/docusaurus.config.*")
      .map { configPath =>
        let sourcePath = configPath
          .trimSuffix("docusaurus.config.ts")
          .trimSuffix("docusaurus.config.js")
          .trimSuffix("/")
        let sourcePath = if (sourcePath == "") { "." } else { sourcePath }
        let pkgDir = ws.directory(sourcePath, include: ["package.json"])
        let pkgJson = if (pkgDir.stat("package.json") != null) {
          pkgDir.file("package.json").asJSON
        } else {
          null
        }
        Site(sourcePath: sourcePath, pkgJson: pkgJson)
      }
  }
}

"""
A Docusaurus site.
"""
type Site {
  """
  Path to the site directory, relative to workspace rootfs.
  """
  pub sourcePath: String!

  """
  Parsed package.json for the site.
  """
  let pkgJson: JSONValue

  """
  The package manager command (read from package.json "packageManager" field).
  """
  let pm: String! {
    if (pkgJson == null) {
      "npm"
    } else {
      let pmField = pkgJson.field(["packageManager"])
      if (pmField == null) {
        "npm"
      } else {
        # packageManager field is like "pnpm@8.15.0" â€” take the name before @
        pmField.asString.split("@")[0] ?? "npm"
      }
    }
  }

  """
  The install command for the package manager.
  """
  let installCmd: [String!]! {
    case (pm) {
      "yarn" => ["yarn", "install", "--frozen-lockfile"]
      "pnpm" => ["pnpm", "install", "--frozen-lockfile"]
      else => ["npm", "ci"]
    }
  }

  """
  Extra source include paths from package.json "docusaurus.include".
  """
  let extraInclude: [String!]! {
    let ds = if (pkgJson != null) {
      pkgJson.field(["docusaurus", "include"])
    } else { null }
    if (ds == null) {
      []::[String!]!
    } else {
      ds.asArray.{asString}.map { item => item.asString }
    }
  }

  """
  Extra source exclude paths from package.json "docusaurus.exclude".
  """
  let extraExclude: [String!]! {
    let ds = if (pkgJson != null) {
      pkgJson.field(["docusaurus", "exclude"])
    } else { null }
    if (ds == null) {
      []::[String!]!
    } else {
      ds.asArray.{asString}.map { item => item.asString }
    }
  }

  """
  The Docusaurus site source files.
  """
  pub source(ws: Workspace!): Directory! {
    ws.directory(
      sourcePath,
      include: extraInclude,
      exclude: [
        "node_modules/",
        ".docusaurus/",
        "build/",
        ".git/",
      ] + extraExclude,
    )
  }

  """
  Base container for docusaurus.
  """
  let base: Container! {
    container
      .from("node:lts-alpine")
      .withoutEntrypoint
      .withWorkdir("/app")
      .withExec(["corepack", "enable"])
      .withMountedCache(
        "/root/.npm/_cacache",
        cacheVolume("npm-cache"),
      )
      .withMountedCache(
        "/usr/local/share/.cache/yarn",
        cacheVolume("yarn-cache"),
      )
      .withMountedCache(
        "/root/.local/share/pnpm/store",
        cacheVolume("pnpm-cache"),
      )
  }

  """
  Container with source mounted and dependencies installed.
  """
  pub sandbox(ws: Workspace!): Container! {
    base
      .withMountedDirectory("/app", source(ws))
      .withMountedCache(
        "./node_modules/.cache",
        cacheVolume("docusaurus-cache-" + sourcePath),
        sharing: CacheSharingMode.PRIVATE,
      )
      .withExec(installCmd)
  }

  """
  Build the static site.
  """
  pub build(ws: Workspace!): Directory! {
    sandbox(ws)
      .withExec(["./node_modules/.bin/docusaurus", "build"])
      .directory("build")
  }

  """
  Serve the static site with nginx.
  """
  pub serve(site: Directory!): Service! {
    container
      .from("nginx:alpine")
      .withDirectory("/usr/share/nginx/html", site)
      .withExposedPort(80)
      .asService
  }
}
