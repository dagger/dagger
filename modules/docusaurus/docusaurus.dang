"""
A Dagger module for docusaurus, a documentation website builder. https://docusaurus.io
"""
type Docusaurus {
  pub sites(ws: Workspace!): [Site!]! {
    ws
      .directory(".", include: [
        "**/docusaurus.config.ts",
        "**/docusaurus.config.js",
      ])
      .glob("**/docusaurus.config.*")
      .map { configPath =>
        let path = configPath
          .trimSuffix("docusaurus.config.ts")
          .trimSuffix("docusaurus.config.js")
          .trimSuffix("/")
        let path = if (path == "") { "." } else { path }
        let pkgDir = ws.directory(path, include: ["package.json"])
        let pkgJson = if (pkgDir.stat("package.json") != null) {
          pkgDir.file("package.json").asJSON
        } else {
          null
        }
        Site(path: path, packageJson: PackageJson(json: pkgJson))
      }
  }
}

"""
A Docusaurus site.
"""

"""
Package.json helpers.
"""
type PackageJson {
  let json: JSONValue

  let hasField(obj: JSONValue!, key: String!): Boolean! {
    obj.fields.filter { field => field == key }.length > 0
  }

  """
  The package manager command (read from package.json "packageManager" field).
  """
  pub packageManager: String! {
    if (json == null) {
      "npm"
    } else if (hasField(json, "packageManager") == false) {
      "npm"
    } else {
      let pmField = json.field(["packageManager"])
      # packageManager field is like "pnpm@8.15.0" â€” take the name before @
      pmField.asString.split("@")[0] ?? "npm"
    }
  }

  """
  Optional source include paths from package.json "docusaurus.include".
  """
  pub include: [String!]! {
    if (json == null) {
      [] :: [String!]!
    } else if (hasField(json, "docusaurus") == false) {
      [] :: [String!]!
    } else {
      let docusaurus = json.field(["docusaurus"])
      if (hasField(docusaurus, "include") == false) {
        [] :: [String!]!
      } else {
        let include = docusaurus.field(["include"])
        include.asArray.{asString}.map { item => item.asString }
      }
    }
  }

  """
  Source exclude paths from package.json "docusaurus.exclude".
  """
  pub exclude: [String!]! {
    if (json == null) {
      [] :: [String!]!
    } else if (hasField(json, "docusaurus") == false) {
      [] :: [String!]!
    } else {
      let docusaurus = json.field(["docusaurus"])
      if (hasField(docusaurus, "exclude") == false) {
        [] :: [String!]!
      } else {
        let exclude = docusaurus.field(["exclude"])
        exclude.asArray.{asString}.map { item => item.asString }
      }
    }
  }

  """
  Whether package.json "docusaurus.nginxConfig" is explicitly set.
  """
  pub hasNginxConfig: Boolean! {
    if (json == null) {
      false
    } else if (hasField(json, "docusaurus") == false) {
      false
    } else {
      let docusaurus = json.field(["docusaurus"])
      hasField(docusaurus, "nginxConfig")
    }
  }

  """
  Path to nginx config from package.json "docusaurus.nginxConfig".
  Defaults to "./nginx.conf" when the field is not set.
  """
  pub nginxConfig: String! {
    if (hasNginxConfig == false) {
      "./nginx.conf"
    } else {
      let docusaurus = json.field(["docusaurus"])
      docusaurus.field(["nginxConfig"]).asString
    }
  }
}

type Site {
  """
  Path of the site directory in the workspace
  """
  pub path: String!

  """
  Parsed package.json helpers for the site.
  """
  let packageJson: PackageJson!

  """
  The package manager command.
  """
  let pm: String! {
    packageJson.packageManager
  }

  """
  Path to the nginx config file in the workspace.
  """
  let nginxConfigPath: String! {
    let cfgPath = packageJson.nginxConfig.trimPrefix("./")
    if (path == ".") {
      cfgPath
    } else {
      path + "/" + cfgPath
    }
  }

  """
  The install command for the package manager.
  """
  let installCmd(ws: Workspace!): [String!]! {
    case (pm) {
      "yarn" => ["yarn", "install", "--frozen-lockfile"]
      "pnpm" => ["pnpm", "install", "--frozen-lockfile"]
      else => ["npm", "install"]
    }
  }

  """
  The Docusaurus site source files.
  """
  pub source(ws: Workspace!): Directory! {
    # include is optional for ws.directory; pass null when no patterns are set
    let include = if (packageJson.include.length == 0) {
      null
    } else {
      packageJson.include.map { inc => path + "/" + inc }
    }

    ws.directory(
      ".",
      include: include,
      exclude: [
        "node_modules/",
        ".docusaurus/",
        "build/",
        ".git/",
      ] + packageJson.exclude.map { exc => path + "/" + exc },
    )
  }

  """
  Base container for docusaurus.
  """
  let base: Container! {
    container
      .from("node:lts-alpine")
      .withoutEntrypoint
      .withWorkdir("/app")
      .withExec(["corepack", "enable"])
      .withMountedCache(
        "/root/.npm/_cacache",
        cacheVolume("npm-cache"),
      )
      .withMountedCache(
        "/usr/local/share/.cache/yarn",
        cacheVolume("yarn-cache"),
      )
      .withMountedCache(
        "/root/.local/share/pnpm/store",
        cacheVolume("pnpm-cache"),
      )
  }

  """
  Container with source mounted and dependencies installed.
  """
  pub sandbox(ws: Workspace!): Container! {
    base
      .withMountedDirectory("/app", source(ws))
      .withWorkdir("/app/" + path)
      .withMountedCache(
        "./node_modules/.cache",
        cacheVolume("docusaurus-cache-" + path),
        sharing: CacheSharingMode.PRIVATE,
      )
      .withExec(installCmd(ws))
  }

  """
  Build the static site.
  """
  pub build(ws: Workspace!): Directory! {
    sandbox(ws)
      .withExec(["./node_modules/.bin/docusaurus", "build"])
      .directory("build")
  }

  """
  Serve the static site with nginx.
  """
  pub server(ws: Workspace!): Service! {
    let ctr = container
      .from("nginx:alpine")
      .withDirectory("/usr/share/nginx/html", build(ws))

    if (packageJson.hasNginxConfig) {
      ctr = ctr.withFile(
        "/etc/nginx/conf.d/default.conf",
        ws.file(nginxConfigPath),
      )
    } else {
      let defaultConfig = ws
        .directory(path, include: ["nginx.conf"])
        .stat("nginx.conf")
      if (defaultConfig != null) {
        ctr = ctr.withFile(
          "/etc/nginx/conf.d/default.conf",
          ws.file(nginxConfigPath),
        )
      }
    }

    ctr
      .withExposedPort(80)
      .asService
  }
}
