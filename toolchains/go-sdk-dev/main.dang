pub description = "Toolchain to develop the Dagger Go SDK"

type GoSdkDev {
  """
  Workspace with all the files needed to develop the SDK
  """
  pub workspace: Directory! @defaultPath(path: "/") @ignorePatterns(patterns: [
    "*",
    "!sdk/go",
    "!go.mod",
    "!go.sum",
    "!cmd/codegen",
    "!engine/slog",
    "!engine/distconsts",
    "!internal/fsutil",
  ])

  """
  Path of the Go SDK source within the workspace
  """
  pub sourcePath: String! = "sdk/go"

  """
  Build a container to run the go toolchain
  """
  pub devContainer: Container! {
    let ctr = go(source: workspace)
      .env
      .withWorkdir(sourcePath)
    # "sdk" is an arbitrary name for state persistence
    engineDev.installClient(ctr, engineDev.service("sdk"))
  }

  pub source: Directory! {
    workspace.directory(sourcePath)
  }

  """
  Test the Go SDK
  """
  pub test: Void {
    devContainer
      .withExec(["go", "test", "-v", "-skip=TestProvision", "./..."])
      .sync

    null
  }

  """
  Regenerate the Go SDK API
  """
  pub generate: Changeset! @generate {
    devContainer
      .withExec(["go", "generate", "-v", "./..."])
      .withExec(["go", "mod", "tidy"])
      .directory("../..") # pop back to repo root
      .changes(workspace)
  }

  """
  Check that releasing works, without actually releasing
  """
  pub releaseDryRun(
    """
    Git repository to fake-release
    """
    sourceRepo: GitRepository! @defaultPath(path: "/"),
    """
    Git tag to fake-release
    """
    sourceTag: String! = "HEAD",
    """
    Git remote to fake-release to
    """
    destRemote: String! = "https://github.com/dagger/dagger-go-sdk.git",
    callback: File! @defaultPath(path: "./git-filter-callback.py"),
  ): Void {
    let version = sourceTag.trimPrefix(sourcePath)
    gitReleaser.dryRun(
      sourceRepo: sourceRepo,
      sourceTag: sourceTag,
      destRemote: destRemote,
      destTag: version,
      callback: callback,
    )
  }

  """
  Publish the Go SDK
  """
  pub release(
    """
    Source git repository to release
    """
    sourceRepo: GitRepository! @defaultPath(path: "/"),
    """
    Source git tag to release
    """
    sourceTag: String!,
    """
    Target git remote to release to
    """
    destRemote: String! = "https://github.com/dagger/dagger-go-sdk.git",
    githubToken: Secret,
    callback: File! @defaultPath(path: "./git-filter-callback.py"),
  ): Void {
    let version = sourceTag.trimPrefix(sourcePath)
    gitReleaser.release(
      sourceRepo: sourceRepo,
      sourceTag: sourceTag,
      destRemote: destRemote,
      destTag: version,
      sourcePath: sourcePath,
      callback: callback,
      githubToken: githubToken,
    )
  }

  let versionFromTag(tag: String!): String! {
    tag.trimPrefix(sourcePath.trimRight("/") + "/")
  }

  """
  Bump the Go SDK's Engine dependency
  """
  pub bump(version: String!): Changeset! {
    # trim leading v from version
    let trimmedVersion = version.trimPrefix("v")

    let versionFile = "// Code generated by dagger. DO NOT EDIT.\n\npackage engineconn\n\nconst CLIVersion = \"" + trimmedVersion + "\"\n"

    let layer = workspace.withNewFile("sdk/go/engineconn/version.gen.go", versionFile)
    layer.changes(workspace)
  }
}
