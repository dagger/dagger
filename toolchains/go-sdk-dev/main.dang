"""Toolchain to develop the Dagger Go SDK"""
type GoSdkDev {
  """
  Workspace with all the files needed to develop the SDK
  """
  pub workspace: Directory! @defaultPath(path: "/") @ignorePatterns(patterns: [
    "*"
    "!sdk/go"
    "!go.mod"
    "!go.sum"
    "!/cmd/codegen"
    "!/engine/slog"
  ])

  """
  Path of the Go SDK source within the workspace
  """
  pub sourcePath: String! = "sdk/go"

  """
  Build a container to run the go toolchain
  """
  pub devContainer(): Container! {
    let ctr = go(source: source()).
      env().
      withWorkdir(sourcePath)
    # "sdk" is an arbitrary name for state persistence
    let devEngine = daggerEngine().service("sdk")
    daggerEngine().installClient(ctr, devEngine)
  }

  pub source(): Directory! {
    workspace.directory(sourcePath)
  }

  """
  Test the Go SDK
  """
  pub test(): MyCheckStatus! {
    devContainer().
      withExec(["go", "test", "-v", "-skip=TestProvision", "./..."]).
      sync
    MyCheckStatus.COMPLETED
  }

  """
  Regenerate the Go SDK API
  """
  pub generate(): Changeset! {
    devContainer().
      withExec(["go", "generate", "-v", "./..."]).
      withExec(["go", "mod", "tidy"]).
      directory("../.."). # pop back to repo root
      changes(source())
  }

  """
  Check that releasing works, without actually releasing
  """
  pub releaseDryRun(
    """Git repository to fake-release"""
    sourceRepo: GitRepository! @defaultPath(path:"/"),
    """Git tag to fake-release"""
    sourceTag: String! = "HEAD",
    """Git remote to fake-release to"""
    dest: String! = "https://github.com/dagger/dagger-go-sdk.git",
    callback: File! @defaultPath(path:"./git-filter-callback.py"),
  ): MyCheckStatus! {
    let version = sourceTag.trimPrefix(sourcePath)
    gitReleaser().dryRun(
      sourceRepo,
      dest: dest,
      sourceTag: sourceTag,
      destTag: version,
      callback: callback
    )
    MyCheckStatus.COMPLETED
  }

  """
  Release the Go SDK
  """
  pub release(
    """Git repository to publish from"""
    sourceRepo: GitRepository! @defaultPath(path:"/"),
    """The git tag to release from"""
    sourceTag: String!,
    """The git remote to publish to"""
    destRemote: String! = "https://github.com/dagger/dagger-go-sdk.git",
    """Optional Github authentication token"""
    githubToken: Secret = null,
    callback: File! @defaultPath(path:"./go-git-filter-callback.py"),
  ): Void {
    # FIXME: path.clean(sourcePath) + "/"
    let version = sourceTag.trimPrefix("sdk/go/")
    gitReleaser().release(
      sourceRepo: sourceRepo,
      sourceTag: sourceTag,
      destRemote: destRemote,
      sourcePath: sourcePath,
      callback: callback,
      destTag: version,
      githubToken: githubToken
    )
    null
  }

  """
  Bump the Go SDK's Engine dependency
  """
  pub bump(version: String!): Changeset! {
    # trim leading v from version
    let trimmedVersion = version.trimPrefix("v")

    let versionFile = "// Code generated by dagger. DO NOT EDIT.\n\npackage engineconn\n\nconst CLIVersion = \"" + trimmedVersion + "\"\n"

    let layer = workspace.withNewFile("sdk/go/engineconn/version.gen.go", versionFile)
    layer.changes(workspace)
  }
}

enum MyCheckStatus { COMPLETED }
