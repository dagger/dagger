pub description = "Toolchain to develop the Dagger Typescript SDK"

type TypescriptSdkDev {

  pub nodeVersionPrevLts: String! = "20.18.1"
  pub nodeVersionLts  = "22.11.0"
	pub bunVersion = "1.1.38"

	pub workspace: Directory!
	 @defaultPath(path:"/")
	 @ignorePatterns(patterns:[
	  "*"
    "!sdk/typescript"
	 ])

	pub sourcePath: String! ="sdk/typescript"

	pub source: Directory! {
	  workspace.directory(sourcePath)
	}

	"""Check the formatting of the SDK"""
	pub lintTypescript: Void! @check {
	  nodejsDevContainer().
			withExec(["yarn", "--cwd", sourcePath, "lint"]).sync
		null
	}

	"""Check the formatting of Typescript docs snippets"""
	pub lintDocsSnippets(
	  docsWorkspace: Directory! @defaultPath(path:"/") @ignorePatterns(patterns:[
			  "*"
				"!**/*.mts"
				"!**/*.mjs"
				"!**/*.ts"
				"!**/*.js"
				"!**/*prettier*"
				"!**/*eslint*"
		  ])
	): Void! @check {
	 let path = "docs/current_docs"
   nodejsDevContainer().
   # Add docs snippets on top of the already-mounted SDK source
   withDirectory(".", docsWorkspace).
   withExec(["yarn", "--cwd", sourcePath, "docs:lint"]).
   sync
   null
  }

	"""Test the SDK with LTS version of Node"""
	pub testNodejsLts: Void! @check {
   testNodejs(nodeVersionLts)
	}

	"""Test the SDK with previous LTS version of Node"""
	pub testNodejsPrevLts: Void! @check {
	  testNodejs(nodeVersionPrevLts)
	}

	"""Test the SDK with the given version of NodeJS"""
	pub testNodejs(nodeVersion: String!): Void! {
    daggerEngine().installClient(nodejsDevContainer(nodeVersion)).
    withExec(["yarn", "--cwd", sourcePath, "test:node", "-i", "-g", "Automatic Provisioned CLI Binary"]).
    sync
    null
	}

	"""Test the SDK with BunJS"""
	pub testBunjs: Void! @check {
    daggerEngine().installClient(bunjsDevContainer()).
    withWorkdir(sourcePath).
    withExec(["bun", "test:bun", "-i", "-g", "Automatic Provisioned CLI Binary"]).
    sync
   null
	}

	"""Generate the Typescript client library"""
	pub generate: Changeset! {
	  let ctr=nodejsBase(nodeVersionPrevLts).
     withMountedDirectory(".", workspace).
     withExec(["yarn", "--cwd", sourcePath, "install"])
    ctr=daggerEngine().installClient(ctr)
    ctr.
    withFile("/usr/local/bin/codegen", codegen().binary()).
		withExec(["codegen", "generate-library", "--lang", "typescript", "-o", "./sdk/typescript/src/api/"]).
	  withExec(["yarn", "--cwd", sourcePath, "eslint", "--max-warnings=0", "--fix", "./src/api/"]).
		directory(".").
		# FIXME: more efficient way to exclude node_modules from the diff?
		withoutDirectory(sourcePath + "/node_modules").
		# FIXME: since we know this is purely additive, compare to empty dir for more efficient diff?
		changes(workspace)
	}

	pub releaseDryRun: Void! @check {
	  release(
			dryRun:true,
			sourceTag:"HEAD",
			npmToken:null,
		)
	}

 pub release(
   """Source git tag to release"""
   sourceTag: String!,
   """NPM authentication token"""
   npmToken: Secret!,
   """Execute a dry-run release, with no side effects"""
   dryRun: Boolean,
 ): Void! {
   let version=versionFromTag(sourceTag)
   let versionFlag =
    if(isSemver(version)) {
     version.trimPrefix("v")
    } else {
      "prepatch"
   }
   let build = nodejsDevContainer.
    withWorkdir(sourcePath).
    withExec(["npm", "run", "build"]).
    withExec(["npm", "version", versionFlag])
   # Check that dist/ exists in the build
   build.directory("dist").entries
   let publishArgs=["npm", "publish", "--access", "public"]
   let publish = if (dryRun == true) {
      build.withExec(publishArgs + ["--dry-run"])
   } else {
     let npmrc=[
       "//registry.npmjs.org/:_authToken=" + npmToken.plaintext,
       "registry=https://registry.npmjs.org/",
       "always-auth=true",
     ].join("\n")
     build.
     withMountedSecret(".npmrc", setSecret("npmrc", npmrc)).
     withExec(publishArgs)
   }
   publish.sync
   null
 }

 pub isSemver(version: String!): Boolean! {
     let binary=go(source:currentModule.source.directory("is-semver")).binary("is-semver")
     let exitCode=container.
      from("alpine").
      withFile("/bin/is-semver", binary).
      withExec(["is-semver", version], expect: ReturnType.ANY).
      exitCode
     exitCode == 0
 }


 let versionFromTag(tag: String!): String! {
   tag.trimPrefix(sourcePath.trimRight("/") + "/")
 }


 """Bump the Typescript SDK's engine dependency"""
 pub bump(version: String!): Changeset! {
    let number=version.trimPrefix("v")
    let contents = [
      "// Code generated by dagger. DO NOT EDIT.",
      "export const CLI_VERSION = \"" + number + "\"",
    ].join("\n")
    workspace.
     withNewFile(sourcePath + "/src/provisioning/default.ts", contents).
     changes(workspace)
 }

# Build a nodejs dev environment, with the SDK source code mounted and its dependencies installed
# FIXME: use baseNodejs() to avoid duplication
pub nodejsDevContainer(nodeVersion: String! =nodeVersionPrevLts): Container! {
	container.
		# ⚠️  Keep this in sync with the engine version defined in package.json
		from("node:" + nodeVersion + "-alpine").
		withMountedCache("/usr/local/share/.cache/yarn", cacheVolume("yarn_cache:" + nodeVersion)).
		withWorkdir("/app").
		withFile(sourcePath + "/package.json", workspace.file(sourcePath + "/package.json")).
		withFile(sourcePath + "/yarn.lock", workspace.file(sourcePath + "/yarn.lock")).
		withFile(sourcePath + "/eslint.config.js", workspace.file(sourcePath + "/eslint.config.js")).
		withExec(["yarn", "--cwd", sourcePath, "install"]).
		withDirectory(".", workspace)
}

# Build a nodejs base image, without any source code mounted
pub nodejsBase(nodeVersion: String! = nodeVersionPrevLts): Container! {
  container.
  from("node:"+nodeVersion+"-alpine").
	withMountedCache("/usr/local/share/.cache/yarn", cacheVolume("yarn_cache:"+nodeVersion)).
  withWorkdir("/app")
}

pub bunjsDevContainer: Container! {
  container.
    from("oven/bun:"+bunVersion+"-alpine").
    withMountedCache("/root/.bun/install/cache", cacheVolume("bun_cache")).
    withWorkdir("/app").
    withFile(sourcePath + "/package.json", workspace.file(sourcePath + "/package.json")).
    withWorkdir(sourcePath).
    withExec(["bun", "install"]).
    withWorkdir("/app").
    withDirectory(".", workspace)
}


  pub source: Directory! @defaultPath(path: "/") @ignorePatterns(patterns: [
    "*"
    "!cmd/codegen"
    "!go.mod"
    "!go.sum"
    "!**/*.go"
    "!sdk/go"
    "!**/*.graphql"
  ])

  pub binary: File! {
    go(source).binary("./cmd/codegen", noSymbols:true, noDwarf:true)
  }
}
